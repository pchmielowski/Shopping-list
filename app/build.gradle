buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.3'
    }
}

apply plugin: 'com.android.application'

apply plugin: 'jacoco-android'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

def commitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion rootProject.ext.sdk_version
    defaultConfig {
        applicationId "net.chmielowski.shoppinglist"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.sdk_version
        versionCode 3
        versionName new Date().format('yyyy.MM.dd') + "." + commitHash()
        testInstrumentationRunner "net.chmielowski.shoppinglist.TestRunner"
        dexOptions {
            preDexLibraries = true
        }
    }
    buildTypes {
        debug {
            aaptOptions {
                cruncherEnabled = false
            }
        }
    }
}

dependencies {
    def koin = rootProject.ext.koin_version
    implementation "org.koin:koin-android:$koin"
    implementation("org.koin:koin-android-viewmodel:$koin") {
        // koin 1.0.2 includes androidx.lifecycle 2.0.0-rc1 which conflicts with the 2.0.0 version
        exclude group: 'androidx.lifecycle'
    }
    implementation "org.koin:koin-core-ext:$koin"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rx_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.ext.rx_kotlin_version"

    implementation 'io.sentry:sentry-android:1.7.16'
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"

    implementation 'com.facebook.stetho:stetho:1.5.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "com.google.android.material:material:1.0.0"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$rootProject.ext.coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.coroutines_version"

    implementation project(path: ':view')
    implementation project(path: ':persistence')
    implementation project(path: ':domain')
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}